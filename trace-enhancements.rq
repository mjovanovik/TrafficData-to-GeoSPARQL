prefix traces: <http://www.tomtom.com/ontologies/traces#>
prefix geo:	<http://www.opengis.net/ont/geosparql#>
prefix sf: <http://www.opengis.net/ont/sf#>

INSERT {
	GRAPH <myGraphIRI> {
		?trace rdf:type geo:Feature .
		?trace geo:hasGeometry ?traceGeomID .
		?traceGeomID a sf:LineString .
		?traceGeomID geo:asWKT ?wkt . 
	}
}
WHERE {
	SELECT ?trace ?traceGeomID strdt(concat('LINESTRING (', group_concat(?wktPoint; separator=", "), ')'), geo:wktLiteral) as ?wkt
	WHERE {
		{ 
			SELECT ?trace ?timestamp ?wktPoint
			WHERE {
				GRAPH <myGraphIRI> {
					?trace a traces:Trace ;
						traces:hasPoint ?point .
					?point traces:hasTimestamp ?timestamp ;
						geo:hasGeometry ?pointGeomID .
					?pointGeomID geo:asWKT ?wkt .
					bind(replace(replace(str(?wkt), 'POINT\\(', '', 'i'), '\\)', '', 'i') as ?wktPoint)
				}
			} 
			ORDER BY ?trace ?timestamp
		}
		bind(iri(concat(str(?trace), '_geom')) as ?traceGeomID)
		
	}
	GROUP BY ?trace ?traceGeomID
}